:PROPERTIES:
:ID:       6b70dbe3-3ce5-4ae4-bee0-db218fbfa337
:END:
#+begin_src emacs-lisp
;;; -*- lexical-binding: t -*-
#+end_src

* Appearance
:PROPERTIES:
:ID:       c5e5d2ae-4e6e-4710-9855-d8eeebdca7df
:END:

#+begin_src emacs-lisp
(setq doom-theme 'doom-tomorrow-night)
#+end_src

Browse https://github.com/hlissner/emacs-doom-themes for more.

* Configure vanilla emacs behavior
** Enable up-/downcase-region

#+begin_src emacs-lisp
(put 'downcase-region 'disabled nil)
(put 'upcase-region 'disabled nil)
#+end_src

** For dired
:PROPERTIES:
:ID:       e37a7ba9-5869-43c6-a134-12ec9e75cde2
:END:

RET uses xdg-open.

#+begin_src emacs-lisp
(defvar unsupported-mime-types
  '("video/mp4"))

(load "subr-x")

(defun get-mimetype (filepath)
  (string-trim
   (shell-command-to-string (concat "file -b --mime-type " (shell-quote-argument filepath)))))

(defun dired-find-file-dwim ()
  (interactive)
  (let ((file (dired-get-filename nil t)))
    (if (member (get-mimetype file) unsupported-mime-types)
      (call-process "xdg-open" nil 0 nil file)
      (find-file file))))

(with-eval-after-load 'dired
  (define-key dired-mode-map (kbd "RET") #'dired-find-file-dwim))
#+end_src

** Set the scratch buffer's major mode to org-mode

#+begin_src emacs-lisp
(setq initial-major-mode 'org-mode)
(setq initial-scratch-message nil)
#+end_src

** ~C-p~ switches to pop-up

#+begin_src emacs-lisp
(map! "C-x p" 'other-popup)
#+end_src

* Configure company

Don't auto-complete in org-mode.

#+begin_src emacs-lisp
(setq company-global-modes (append company-global-modes '(org-mode)))
#+end_src

* Configure deft
:PROPERTIES:
:ID:       15a57748-c59d-4005-8629-c706337e4542
:END:

The default Deft buffer shows on the left-hand side the head of the file, and on the right the last modified time.
However, the head of Org-roam files are not informative at all, because they hold the ~:PROPERTIES:~ tag.
So, show the filename at the left-hand side instead.

#+begin_src emacs-lisp
(setq deft-directory "~/roamdb/notes"
      deft-recursive t)
#+end_src

Also add [[https://github.com/jrblevin/deft/issues/75#issuecomment-905031872][a fix from GitHub user codemac]] for deft to correctly parse titles from org-roam v2 files.

#+begin_src emacs-lisp
(defun cm/deft-parse-title (file contents)
  "Parse the given FILE and CONTENTS and determine the title.
  If `deft-use-filename-as-title' is nil, the title is taken to
  be the first non-empty line of the FILE.  Else the base name of the FILE is
  used as title."
  (let ((begin (string-match "^#\\+[tT][iI][tT][lL][eE]: .*$" contents)))
    (if begin
        (string-trim (substring contents begin (match-end 0))
                     "#\\+[tT][iI][tT][lL][eE]: *" "[\n\t ]+")
      (deft-base-filename file))))

(advice-add 'deft-parse-title :override #'cm/deft-parse-title)
(setq deft-strip-summary-regexp
      (concat "\\("
              "[\n\t]" ;; blank
              "\\|^#\\+[[:alpha:]_]+:.*$" ;; org-mode metadata
              "\\|^:PROPERTIES:\n\\(.+\n\\)+:END:\n"
              "\\)"))
#+end_src

* Configure ESS

Don't indent "#" comments (https://stackoverflow.com/a/780929/6910451).

#+begin_src emacs-lisp
(setq ess-fancy-comments nil)
#+end_src

Enable the visual vertical ruler at 80 characters.

#+begin_src emacs-lisp
(add-hook 'ess-r-mode-hook 'display-fill-column-indicator-mode)
#+end_src

* Configure org
** Appearance
:PROPERTIES:
:ID:       ae2762e5-453b-4daf-b850-45ca6fe17675
:END:

Collapse code chunks by default.
Both variables need to be set ([[https://web.archive.org/web/20210912005440/https://orgmode.org/list/87wntgaz5u.fsf@kyleam.com/]["Re: "#+STARTUP: hideblocks" has no effect?"]])

#+begin_src emacs-lisp
(setq org-hide-block-startup t
      org-startup-folded 'nofold)
#+end_src

Align and shrink tables on start-up.

#+begin_src emacs-lisp
(setq org-startup-align-all-tables t
      org-startup-shrink-all-tables t)
#+end_src

** Behavior

Export files to the Download directory, rather than the source file's directory ([[https://stackoverflow.com/a/47850858/6910451][copied from StackExchange]]):

#+begin_src emacs-lisp
(defun org-export-output-file-name-modified (orig-fun extension &optional subtreep pub-dir)
  (unless pub-dir
    (setq pub-dir "~/Downloads")
    (unless (file-directory-p pub-dir)
      (make-directory pub-dir)))
  (apply orig-fun extension subtreep pub-dir nil))
(advice-add 'org-export-output-file-name :around #'org-export-output-file-name-modified)
#+end_src

** Custom structure templates:

#+begin_src emacs-lisp
(use-package! org-tempo
  :config
  (tempo-define-template "src-bash"
                         '("#+begin_src bash :results output\n" p "\n#+end_src" >)
                         "<S" "Insert a bash src block")
  (tempo-define-template "from-pubmed"
                         '("Discovered via PubMed search alert for ~(Duke-NUS[Affiliation]) AND ((Emerging Infectious Diseases[Affiliation]) OR (Cancer and Stem Cell Biology[Affiliation]) OR (Cardiovascular and Metabolic Disorders[Affiliation]) OR (Centre for Computational Biology[Affiliation]))~.")
                         "<lp" "Describe discovery via PubMed search alert")
  (tempo-define-template "from-science"
                         '("Discovered via the Science Table of Contents email subscription service.")
                         "<ls" "Describe discovery via Science email alert"))
#+end_src

* Configure org-agenda
:PROPERTIES:
:ID:       94a80c46-03e0-4f0d-a4c7-e6e8a55a00e0
:END:

** Set Org-agenda to search Org-roam notes
:PROPERTIES:
:ID:       5bfbb4f4-b7b8-43ef-82a2-c5eb85c4682e
:END:

I keep notes with active tasks in a separate subdirectory to speed up ~Org-agenda~.

#+begin_src emacs-lisp
(setq org-agenda-files '("~/roamdb/notes/tasks"))
#+end_src

** Todo keywords
:PROPERTIES:
:ID:       d8e0acba-e9eb-4f34-8062-9faa907d0356
:END:

Doom Emacs defines a whole bunch of ~TODO~ states, many of which I do not use.
Simplify the list:

#+begin_src emacs-lisp
; Must be eval'd after org: https://github.com/hlissner/doom-emacs/issues/2913
(after! org
  (setq org-todo-keywords
        '((sequence "TODO(t)" "|" "CANCELED(c)" "DONE(d)"))))
#+end_src

When a task is done, mark it with a ~CLOSED~ timestamp.

#+begin_src emacs-lisp
(setq org-log-done "time")
#+end_src

** Agenda view

The default prefix for org-agenda items includes their file names, but org-roam file names are incredibly verbose.
So, prefix each agenda item with just an indentation.

#+begin_src emacs-lisp
(with-eval-after-load 'org-agenda
    (add-to-list 'org-agenda-prefix-format '(agenda . "  ")))
#+end_src

Show tasks starting from yesterday, up to six days from today.

#+begin_src emacs-lisp
(setq org-agenda-start-day "-1d"
      org-agenda-span 8)
#+end_src

Don't dim tasks blocked by subtasks: for me, subtasks represents steps to complete the parent task.
I do not think of them as tasks by themselves.

#+begin_src emacs-lisp
(setq org-agenda-dim-blocked-tasks nil)
#+end_src

org-agenda warns about upcoming deadlines by showing them in today's agenda.
That crowds the daily agenda, in my opinion.

#+begin_src emacs-lisp
(setq org-deadline-warning-days 0
      org-agenda-skip-scheduled-if-deadline-is-shown t)
#+end_src

By default, closed tasks are shown on the date they were scheduled.
I prefer that they are shown on the date they were done.
First, I set ~org-agenda-skip-*-if-done~  to ~nil~ to hide done tasks from their scheduled date, then I use log-mode to show done tasks on the date they were done.

#+begin_src emacs-lisp
(setq org-agenda-skip-scheduled-if-done t
      org-agenda-skip-deadline-if-done t
      org-agenda-start-with-log-mode '(closed))
#+end_src

* Configure org-crypt

See: https://orgmode.org/manual/Org-Crypt.html

#+begin_src emacs-lisp
(use-package! org-crypt
  :config
  (org-crypt-use-before-save-magic)
  (setq org-tags-exclude-from-inheritance '("crypt")
        org-crypt-key "ningyuan.sg@gmail.com"))
#+end_src

* Configure org-download
:PROPERTIES:
:ID:       6dc51b5f-5bff-46a6-90ad-587b1f89749b
:END:

#+begin_src emacs-lisp
(use-package! org-download
  :config
  (setq-default org-download-image-dir "~/roamdb/files"
                org-download-heading-lvl nil))
#+end_src

* Configure org-export

Copied from https://stackoverflow.com/a/37132338/6910451.

#+begin_src emacs-lisp
(defun my-org-inline-css-hook (exporter)
  "Insert custom inline css"
  (when (eq exporter 'html)
    (let* ((dir (ignore-errors (file-name-directory (buffer-file-name))))
           (path (concat dir "style.css"))
           (homestyle (or (null dir) (null (file-exists-p path))))
           (final (if homestyle "~/roamdb/files/org-style.css" path))) ;; <- set your own style file path
      (setq org-html-head-include-default-style nil)
      (setq org-html-head (concat
                           "<style type=\"text/css\">\n"
                           "<!--/*--><![CDATA[/*><!--*/\n"
                           (with-temp-buffer
                             (insert-file-contents final)
                             (buffer-string))
                           "/*]]>*/-->\n"
                           "</style>\n")))))

(add-hook 'org-export-before-processing-hook 'my-org-inline-css-hook)
#+end_src

* Configure org-roam
:PROPERTIES:
:ID:       52bc4886-496f-472c-a273-851511e0a3d4
:END:

Adapted from Jethro at GitHub:jethrokuan/dots.

#+begin_src emacs-lisp
(use-package! org-roam
  :config
  (defun ning/org-roam-file-find-given-filetag (tag)
    (org-roam-node-find nil nil
                        (lambda (node)
                           (and
                            (eq (org-roam-node-level node) 0)
                            (cl-member tag (org-roam-node-tags node) :test #'string=)))))
  (defun ning/org-roam-file-find-given-not-filetag (tag)
    (org-roam-node-find nil nil
                        (lambda (node)
                           (and
                            (eq (org-roam-node-level node) 0)
                            (not (cl-member tag (org-roam-node-tags node) :test #'string=))))))
  (defun ning/org-roam-not-lit-find ()
    (interactive) (ning/org-roam-file-find-given-not-filetag "lit"))
  (defun ning/org-roam-file-find ()
    (interactive)
    (org-roam-node-find nil nil (lambda (node) (eq (org-roam-node-level node) 0))))
  (map! :leader
        :prefix "r"
        ; Finder functions
        :desc "org-roam-not-lit-find" "f" #'ning/org-roam-not-lit-find
        :desc "org-roam-node-find" "F" #'org-roam-node-find
        ; Other functions
        :desc "org-roam-node-insert" "i" #'org-roam-node-insert
        :desc "org-roam-capture" "c" #'org-roam-capture
        :desc "org-roam-tag-add" "a" #'org-roam-tag-add
        :desc "org-roam-dailies-goto-today" "." #'org-roam-dailies-goto-today
        :desc "org-roam-dailies-goto-next-note" "/" #'org-roam-dailies-goto-next-note
        :desc "org-roam-dailies-goto-previous-note" "," #'org-roam-dailies-goto-previous-note)
  (setq org-roam-directory (file-truename "~/roamdb/notes")
        org-id-link-to-org-use-id t
        org-roam-v2-ack t
        org-roam-node-display-template (concat "${title:*}" (propertize "${tags:10}" 'face 'org-tag))
        +org-roam-open-buffer-on-find-file nil
        org-roam-capture-templates '(("n" "note" plain "%?" :target
                                      (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n")
                                      :unnarrowed t :immediate-finish t)
                                     ("r" "review" plain "%?" :target
                                      (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n#+filetags: :review:")
                                      :unnarrowed t :immediate-finish t)
                                     ("t" "task" plain "%?" :target
                                      (file+head "tasks/%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n#+filetags: :task:")
                                      :unnarrowed t :immediate-finish t)
                                     ("l" "lit" plain "%?" :target
                                      (file+head "notes-ref/${citekey}.org" "#+title: ${author-abbrev} ${year} - ${title}\n#+filetags: :lit:")
                                      :unnarrowed t :immediate-finish t)))
  (add-to-list 'display-buffer-alist
               '(("\\*org-roam\\*"
                  (display-buffer-in-direction)
                  (direction . right)
                  (window-width . 0.33)
                  (window-height . fit-window-to-buffer))))
  (org-roam-db-autosync-enable))
#+end_src

* Configure org-roam-ui
:PROPERTIES:
:ID:       1536a96b-f528-4e27-b2e7-ec640762e6e4
:END:

#+begin_src emacs-lisp
(use-package! websocket
    :after org-roam)
(use-package! org-roam-ui
    :after org-roam
    :config (setq org-roam-ui-open-on-start t))
(defun ning/org-roam-ui-browse ()
  "Open org-roam-ui in the browser."
  (interactive)
  (browse-url-xdg-open "http://127.0.0.1:35901/"))
#+end_src

* Configure org-roam-bibtex (incl. org-ref)

#+begin_src emacs-lisp
(use-package! org-ref
  :init (setq doi-utils-download-pdf nil
              bibtex-autokey-year-title-separator nil
              bibtex-autokey-titleword-separator "-")
        (map! :leader
              :prefix "r"
              :desc "org-ref-insert-link" "l" #'org-ref-insert-link)
  :config (require 'bibtex))

(use-package! ivy-bibtex
  :init
  (setq bibtex-completion-bibliography '("~/roamdb/files/references.bib")
        bibtex-completion-notes-path "~/roamdb/notes/notes-ref/"
        bibtex-completion-library-path "~/roamdb/files-ref/"
        ivy-bibtex-default-action 'ivy-bibtex-edit-notes
        ivy-bibtex-default-multi-action 'ivy-bibtex-insert-bibtex)
  ; Sort by last added entry first. Default sort is first added entry first.
  (advice-add 'bibtex-completion-candidates :filter-return 'reverse))

(use-package! org-roam-bibtex
  :after org-roam
  :init (setq orb-roam-ref-format 'org-ref-v3
              orb-preformat-keywords '("citekey" "author-abbrev" "year" "title"))
  :config
  (org-roam-bibtex-mode)
  (require 'org-ref))
#+end_src

To insert a link, use ~C-c r l~ (org-ref-insert-link).
To navigate to a reference-associated note, use ~C-c n b~ (ivy-bibtex) and select a single item (runs ivy-bibtex-edit-notes).
To insert a reference list into the current buffer, use use ~C-c n b~ (ivy-bibtex) and select one or more items (select using ~C <space>~, runs ivy-bibtex-insert-bibtex).

* Configure snakemake-mode

#+begin_src emacs-lisp
(add-hook 'snakemake-mode-hook 'display-fill-column-indicator-mode)
#+end_src

* Configure telega

#+begin_src emacs-lisp
(use-package! telega
  :config (setq telega-use-images nil))
#+end_src

* Configure ui/doom-dashboard
:PROPERTIES:
:ID:       6ac43331-fa55-405e-8db4-2e682a27a11e
:END:

#+begin_src emacs-lisp
(defun +ning/dashboard-date-proportion-passed (start end)
  (/
   (float-time (time-subtract
                (float-time) (org-read-date nil t start)))
   (float-time (time-subtract
                (org-read-date nil t end) (org-read-date nil t start)))))

(defun +ning/dashboard-date-decorated-progress-bar (start end)
  (let* ((prop-passed (+ning/dashboard-date-proportion-passed start end))
         (pbar-width 50)
         (pbar-pluses (min (floor (* prop-passed pbar-width)) pbar-width))
         (pbar-dashes (- pbar-width pbar-pluses))
         (prop-pretty
          (if (> prop-passed 1)
              "100%"
            (format "%4.1f%%" (* 100 prop-passed)))))
    (concat start " ["
            (make-string pbar-pluses ?+)
            (make-string pbar-dashes ?-)
            "|" prop-pretty "] " end)))

(defun ning/dashboard-widget-milestones ()
  (let ((result "\n"))
    (dolist (triplet ning/dashboard-milestones result)
      (setq result (concat result "\n"
                           (+doom-dashboard--center
                            +doom-dashboard--width
                            (car triplet))
                           "\n"
                           (+doom-dashboard--center
                            +doom-dashboard--width
                            (+ning/dashboard-date-decorated-progress-bar
                             (nth 1 triplet) (nth 2 triplet)))
                           "\n")))
    (insert result)))

(setq ning/dashboard-milestones
      '(("Year 2021" "2021-01-01" "2021-12-31")))
(if (file-exists-p "~/roamdb/files/private-dashboard-milestones.el")
    (load "~/roamdb/files/private-dashboard-milestones.el")
  nil)

(setq +doom-dashboard-functions
      '(doom-dashboard-widget-banner
        ning/dashboard-widget-milestones
        doom-dashboard-widget-loaded
        doom-dashboard-widget-footer))
#+end_src

* Configure ui/workspaces

#+begin_src emacs-lisp
(map! :leader
        :prefix "w"
        :desc "+workspace/swap-left" "<left>" #'+workspace/swap-left
        :desc "+workspace/swap-right" "<right>" #'+workspace/swap-right)
#+end_src

* Custom: <C-c f s> switches to the scratch buffer

#+begin_src emacs-lisp
(map!
 (:leader
  (:prefix "f"
   :desc "Open *scratch*" "s" #'(lambda () (interactive) (switch-to-buffer "*scratch*")))))
#+end_src

* Custom: sync notes with unison
:PROPERTIES:
:ID:       d7020545-f73b-44f3-b524-eb8bade4f062
:END:

I had previously done this with rclone with Google Drive, but rclone cannot handle conflicts well --- out of the box, it only syncs whole directories at a time, always overwriting changes.

#+begin_src emacs-lisp
(defun ning/get-time-string ()
  "Get a string, trimmed for white-spaces, like 1.43pm."
  (downcase (string-trim (format-time-string "%l.%M%p"))))

(defun ning/get-unison-sentinel (should-org-roam-setup-flag)
  "Internal. Get a sentinel function."
  (lambda (proc event)
    (if should-org-roam-setup-flag (org-roam-setup) nil)
    (if (string= event "finished\n")
        (message (concat (process-name proc) " done at " (ning/get-time-string)))
      (message
       (propertize (concat (process-name proc) " failed at " (ning/get-time-string))
                   'face '(:foreground "red"))))))

(defun ning/sync-show-unison-log ()
  "Switch to the unison log buffer"
  (interactive)
  (switch-to-buffer "*unison*"))

(defun ning/sync ()
  "Sync the ~/org directory to ssh:roamdb-ctl. Returns the process."
  (interactive)
  ; Delimit different runs with a page break.
  (save-excursion
    (with-current-buffer (get-buffer-create "*unison*")
      (end-of-buffer)
      (insert (concat "\n==" (current-time-string) "==\n"))
      (page-break-lines-mode 1))
    (make-process
     :name "ning/sync"
     :buffer "*unison*"
     :command `("unison" "-batch" ,(expand-file-name "~/roamdb") "ssh://meowmeow//home/admin/roamdb")
     ; Unison breaks lines with carriage returns only, no matter the host OS.
     :coding 'undecided-mac
     :sentinel (ning/get-unison-sentinel t))))

(defun ning/sync-synchronous ()
  "Sync the ~/org directory to ssh:meowmeow. Returns the process."
  (interactive)
  (delete-other-windows)
  (switch-to-buffer (get-buffer-create "*unison*"))
  (end-of-buffer)
  (insert (concat "\n==" (current-time-string) "==\n"))
  (page-break-lines-mode 1)
  (let ((coding-system-for-read 'undecided-mac))
    (call-process "unison" nil "*unison*" t "-batch" (expand-file-name "~/roamdb") "ssh://meowmeow//home/admin/roamdb")))
#+end_src

Sync on start-up, unless on Termux.
On Termux, unison must be run without locks, because otherwise it always thinks that a lock is always present and thus refuses to continue.
Since there are no locks on Termux, I prefer to always sync manually.
This applies also to the sync on idle timers, etc.

#+begin_src emacs-lisp
(defun ning/on-termux-p ()
  (and (getenv "PREFIX") (string-match-p "com.termux" (getenv "PREFIX"))))

(if (not (ning/on-termux-p)) (after! org-roam (ning/sync)))
#+end_src

Sync on idle.

#+begin_src emacs-lisp
(if (not (ning/on-termux-p)) (run-with-idle-timer 60 t 'ning/sync))
#+end_src

Sync on return from idle.

#+begin_src emacs-lisp
(defvar ning/has-idled-past-sync-threshold nil)
(run-with-idle-timer 300 t (lambda () (setq ning/has-idled-past-sync-threshold t)))
(if (not (ning/on-termux-p))
    (add-hook 'post-command-hook
              (lambda () (when ning/has-idled-past-sync-threshold
                           (ning/sync)
                           (setq ning/has-idled-past-sync-threshold nil)))))
#+end_src

Sync on exit.

#+begin_src emacs-lisp
(if (not (ning/on-termux-p))
    (add-hook 'kill-emacs-hook (lambda () (ning/sync-synchronous) (sleep-for 1))))
#+end_src

* Custom: query online databases
:PROPERTIES:
:ID:       b18337f4-dd0f-4381-b35e-b970a36e4f09
:END:

** Search RefSNP for word at point
:PROPERTIES:
:ID:       d28262ec-ede7-43fd-a45e-bde7e6a9157f
:END:

#+begin_src emacs-lisp
(defun ning/refsnp-word-at-point ()
  "Open the NCBI RefSNP page for the word at point (which hopefully, is a RefSNP number)"
  (interactive)
  (browse-url-xdg-open
   (concat "https://www.ncbi.nlm.nih.gov/snp/"
           (thing-at-point 'word 'no-properties))))
#+end_src

** Google-scholar for text in region
:PROPERTIES:
:ID:       d94a0f04-ac3d-4028-b7cb-21e46d9aaa65
:END:

#+begin_src emacs-lisp
(defun ning/gscholar-text-in-region (start end)
  "Search Google Scholar for the text of the selected region"
  (interactive "r")
  (browse-url-xdg-open
   (concat "https://scholar.google.com/scholar?&q="
           (url-hexify-string (buffer-substring start end)))))
#+end_src
